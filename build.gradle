buildscript {
  repositories {
    maven { url "https://plugins.gradle.org/m2/" }
  }
  dependencies {
    classpath(
        "gradle.plugin.com.palantir.gradle.gitversion:gradle-git-version:0.5.3",
        "com.github.ben-manes:gradle-versions-plugin:0.13.0",
        "gradle.plugin.de.fuerstenau:BuildConfigPlugin:1.1.4",
        "net.ltgt.gradle:gradle-apt-plugin:0.13",
        "com.jfrog.bintray.gradle:gradle-bintray-plugin:1.7.3",
    )
  }
}
subprojects {
  apply plugin: "java"
  apply plugin: "maven"
  apply plugin: "idea"
  apply plugin: "signing"
  apply plugin: "com.palantir.git-version"
  apply plugin: "com.github.ben-manes.versions"

  sourceCompatibility = JavaVersion.VERSION_1_7
  targetCompatibility = JavaVersion.VERSION_1_7

  // Set the group and version across all subprojects to ensure consistency
  group 'com.clarifai.clarifai-api2'
  def details = versionDetails()
  def tag = details.lastTag.replace(".dirty", "")
  if (details.commitDistance != 0) {
    tag = tag + '-' + details.gitHash
  }
  version tag
  println("Computed version as: ${getVersion()}")

  repositories {
    mavenLocal()
    jcenter()
    mavenCentral()
    maven { url "https://jitpack.io" }
  }

  ext.versions = [
      okhttp: "3.4.1",
      gson: "2.7",
      autoValue: "1.5.2",
      slf4j: "1.7.21",
      kotlin: "1.0.5-2",

      customHashCodeEquals: "db3442d",
  ]

  ext.deps = [
      nullityAnnotations: "org.jetbrains:annotations:13.0",
      kotlin: [
          stdlib: "org.jetbrains.kotlin:kotlin-stdlib:$versions.kotlin",
          reflect: "org.jetbrains.kotlin:kotlin-reflect:$versions.kotlin",
          gradlePlugin: "org.jetbrains.kotlin:kotlin-gradle-plugin:$versions.kotlin",
      ],
  ]
}

task wrapper(type: Wrapper) {
  gradleVersion = "4.3.1"
}
